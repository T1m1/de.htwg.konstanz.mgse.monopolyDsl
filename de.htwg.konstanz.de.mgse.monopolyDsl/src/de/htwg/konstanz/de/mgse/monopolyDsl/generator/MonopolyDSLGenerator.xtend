/*
 * generated by Xtext 2.9.1
 */
package de.htwg.konstanz.de.mgse.monopolyDsl.generator

import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.AbstractGenerator
import org.eclipse.xtext.generator.IFileSystemAccess2
import org.eclipse.xtext.generator.IGeneratorContext
import de.htwg.konstanz.de.mgse.monopolyDsl.monopolyDSL.Game
import de.htwg.konstanz.de.mgse.monopolyDsl.monopolyDSL.Field
import org.eclipse.emf.common.util.EList

/**
 * Generates code from your model files on save.
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#code-generation
 */
class MonopolyDSLGenerator extends AbstractGenerator {

	val gameType = "monopoly"
	val typeClass = "FieldType"
	val fieldInterface = "IField"

	String gameName

	override void doGenerate(Resource resource, IFileSystemAccess2 fsa, IGeneratorContext context) {

		val game = resource.contents.head as Game

		gameName = game.name

		val fields = game.fields

		if (!fields.isEmpty) {
			// generate an Interface if there are fields
			fsa.generateFile(fieldInterface + ".java", generateInterface())

			// generate Enum class for fieldTypes
			fsa.generateFile(typeClass + ".java", generateFieldTypes(fields, game))
		}

		for (singleField : fields) {
			// create a class for each field
			val class = generateClass(singleField)
			fsa.generateFile(singleField.name.toFirstUpper + ".java", class)
		}
	}

	/**
	 * Generate field enums
	 */
	def generateFieldTypes(EList<Field> list, Game game) '''
		«createPackage()»
		
		
		/**
		 * These enums represent the different types of a certain field in monopoly.
		 * 
		 */
		public enum «typeClass» {
		
		«FOR field : list BEFORE "" SEPARATOR "," AFTER ""»
			/**
			 * «field.description»
			 */
			«field.name.toUpperCase»
			
		«ENDFOR»
		}
		
	'''

	def generateInterface() '''
		«createPackage()»
		
		«createImportOf(gameType)»
		
		
		public interface «fieldInterface» {
			
			/**
			 * return the position of the field
			 * @return integer 
			 */
			int getPosition();
			
			/**
			 * Return the type of this field. For example: Street, train station etc.
			 * @return enum
			 */
			«typeClass» getType();
		}
		
	'''

	def generateClass(Field field) '''
		«createPackage()»
		
		«createImportOf(fieldInterface)»
		«createImportOf(typeClass)»
		
		public class «field.name» implements «fieldInterface» {
			
			private final «typeClass» type;
			private final int position;
			«createOptionalFields(field.optProperties)»
			
			// hide default constructor
			private «field.name.toFirstUpper»() {}
			
			/**
			 * Constructor for «field.name» field in Monopoly
			 */
			«createConstructor(field.name.toFirstUpper, field.optProperties)»
			
			
			«createGetter(field.optProperties)»
			
			«createSetter(field.optProperties)»		
		
			@Override
			public String toString() {
				return «field.name»;
			}
		
			public int getPriceToPay() {
				return priceToPay;
			}
		
			@Override
			public FieldType getType() {
				return type;
			}
		
			@Override
			public int getPosition() {
				return this.guiPosition;
			}
			
			
		
		}
	'''

	def createSetter(EList<String> properties) '''
		«FOR property : properties BEFORE "" SEPARATOR "\n" AFTER ""»
			public void set«property.toFirstUpper»(«getTypeOf(property)») {
				this.«property» = «property»;
				}
				«ENDFOR»	
	'''

	def createGetter(EList<String> properties) '''
		«FOR property : properties BEFORE "" SEPARATOR "\n" AFTER ""»
			public «getOnlyTypeOf(property)» get«property.toFirstUpper»() {
				return this.«property»;
				}
		«ENDFOR»				
	'''

	def createConstructor(String name,
		EList<String> properties) '''
		public «name»(int position, «typeClass» fieldType, «FOR property : properties BEFORE "" SEPARATOR "," AFTER ")"»«getTypeOf(property)»«ENDFOR» {
					«FOR property : properties BEFORE "" SEPARATOR ";" AFTER ";"»
						this.«property» = «property»
					«ENDFOR»
				}
	'''

	def createOptionalFields(EList<String> properties) '''
		«FOR property : properties BEFORE "" SEPARATOR ";" AFTER ";"»
			private final «getTypeOf(property)»
		«ENDFOR»
	'''

	def getTypeOf(String type) '''
		«IF type == "rent"»int rent
		«ELSEIF type == "name"»String name
		«ELSEIF type == "color"»String color
		«ELSEIF type == "price"»int price
		«ELSEIF type == "soldStatus"»boolean sold
		«ELSEIF type == "action"»String action
		«ENDIF»
	'''

	def getOnlyTypeOf(String type) '''
		«IF type == "rent"»int
		«ELSEIF type == "name"»String
		«ELSEIF type == "color"»String
		«ELSEIF type == "price"»int
		«ELSEIF type == "soldStatus"»boolean
		«ELSEIF type == "action"»String
		«ENDIF»
	'''

	def createPackage() '''
		package de.«gameName».«gameType»;
	'''

	def createImportOf(String classToImport) '''
		import de.«gameName».«gameType».«classToImport»;
	'''

}
